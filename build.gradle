import com.jfrog.bintray.gradle.BintrayExtension

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name "jboss-snapshots"
            url "http://snapshots.jboss.org/maven2/"
        }
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
        classpath 'org.hibernate.build.gradle:hibernate-matrix-testing:2.0.0-SNAPSHOT'
        classpath 'org.hibernate.build.gradle:version-injection-plugin:1.0.0'
        classpath 'org.hibernate.build.gradle:gradle-xjc-plugin:1.0.2.Final'
        classpath 'com.github.lburgazzoli:lb-karaf-features-gen:1.0.0-SNAPSHOT'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

plugins {
    id 'base'
    id 'com.gradle.build-scan' version '1.9'
    id 'me.champeau.buildscan-recipes' version '0.1.7'
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            name "jboss-snapshots"
            url "http://snapshots.jboss.org/maven2/"
        }
    }

    apply from: rootProject.file( 'gradle/base-information.gradle' )
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Link together all release tasks

task release {
    doFirst {
        println "Starting version $project.hibernateVersion release"
    }
}

gradle.projectsEvaluated { g ->
	g.rootProject.subprojects { subproject ->
		Task releaseTask = subproject.tasks.findByName( 'release' )
		if ( releaseTask ) {
			releaseTask.dependsOn tasks.release
			tasks.release.dependsOn releaseTask
		}
	}
}


subprojects { subproject ->
    task echoDetails {
        doLast {
            println "[$subproject.name] groupId    : $subproject.group"
            println "[$subproject.name] artifactId : $subproject.name"
            println "[$subproject.name] version    : $subproject.version"
        }
    }
}


subprojects { subproject ->
    subproject.afterEvaluate {
        task bintrayInfo {
            doLast {
                BintrayExtension bintrayExtension = subproject.extensions.findByName( 'bintray' ) as BintrayExtension
                println "BintrayExtension info  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
                println "[$subproject.name] Package name : $bintrayExtension.pkg.name"
                println "[$subproject.name] Package repo : $bintrayExtension.pkg.repo"
                println "[$subproject.name] Version name : $bintrayExtension.pkg.version.name"

                String cfgs = bintrayExtension.configurations ? bintrayExtension.configurations.join( ', ' ) : 'none'
                println "[$subproject.name] Configurations : $cfgs"

                String pubs = bintrayExtension.publications ? bintrayExtension.publications.join( ', ' ) : 'none'
                println "[$subproject.name] Publications : $pubs"
            }
        }
    }
}

task bintrayInfo {}


task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
    distributionType = 'ALL'
}
